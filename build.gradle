/*
 * Copyright 2018 EPAM Systems
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'net.researchgate.release' version '2.8.0'
    id 'com.jfrog.bintray' version '1.8.4'
}
apply plugin: 'java'
apply from: 'release.gradle'
apply from: 'quality.gradle'

sourceCompatibility = 1.8
targetCompatibility = 1.8

project.ext {
    releaseMode = project.hasProperty('releaseMode')
}

description = 'ReportPortal JUnit 4 client'

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven { url 'https://jitpack.io' }
    maven { url "http://dl.bintray.com/epam/reportportal" }
}
dependencies {
    if (releaseMode) {
        compile 'com.epam.reportportal:client-java:4.0.16'
        compile 'com.epam.reportportal:commons-model:5.0.0-BETA-6'
    } else {
        compile 'com.github.reportportal:client-java:d27d103'
        compile 'com.github.reportportal:commons-model:25f966e'
    }
    compile 'com.nordstrom.tools:junit-foundation:9.1.0'
    compile 'com.google.inject:guice:4.1.0'
}
ext {
    junitFoundation = configurations.compile.resolvedConfiguration.resolvedArtifacts.find { it.name == 'junit-foundation' }
}
test.doFirst {
    jvmArgs "-javaagent:${junitFoundation.file}"
}
test {
//  debug true
    testLogging.showStandardStreams = true
}
wrapper {
    gradleVersion = '5.4.1'
}
