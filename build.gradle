/*
 * Copyright 2018 EPAM Systems
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java'
}

project.ext.releaseMode = project.hasProperty('releaseMode')

def branch = releaseMode ? '5.0.0' : 'develop'
apply from: "https://raw.githubusercontent.com/reportportal/gradle-scripts/$branch/build-quality.gradle"
apply from: "https://raw.githubusercontent.com/reportportal/gradle-scripts/$branch/release-commons.gradle"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    releaseMode ? maven { url "http://dl.bintray.com/epam/reportportal" } : maven { url 'https://jitpack.io' }
}
dependencies {
    if (releaseMode) {
        compile 'com.epam.reportportal:client-java:5.0.0-BETA-8'
        compile 'com.epam.reportportal:commons-model:5.0.0'
    } else {
        compile 'com.github.reportportal:client-java:493a382b'
        compile 'com.github.reportportal:commons-model:e608d19'
    }

    compile 'com.nordstrom.tools:junit-foundation:11.2.0'
    testCompile "org.aspectj:aspectjweaver:1.9.2"
    testCompile 'org.mockito:mockito-core:3.2.4'
}
ext {
    junitFoundation = configurations.compile.resolvedConfiguration.resolvedArtifacts.find { it.name == 'junit-foundation' }
}
processResources {
    filesMatching('agent.properties') {
        expand(project.properties)
    }
}
test.doFirst {
    def weaver = configurations.compile.find { it.name.contains("aspectjweaver") }
    jvmArgs += "-javaagent:$weaver"
    jvmArgs += "-javaagent:${junitFoundation.file}"
}
test {
//  debug true
    testLogging.showStandardStreams = true
    exclude 'com/epam/reportportal/junit/ExpectToFail.class'
}
wrapper {
    gradleVersion = '5.4.1'
}

afterReleaseBuild.dependsOn bintrayUpload
